      SUBROUTINE SYMLLOYD(DETALF,AR,NATREF,NATPER,LMAXSQ,LMAX,
     +                    NATPS,NEND,NREP,INS,NDIM,IREF,IRMIN,
     +                    ASARY,ISPIN,IE,NDG,NSHELL)
      IMPLICIT NONE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C                                                                      C
C--> THIS SUBROUTINE CALCULATES THE SYMMETRIC PARTS OF THE             C
C    DETERMINANT OF THE AR - MATRICES. THIS IS USED IN CASE            C
C    OF SYMMETRYDECOMPOSITION.                                         C
C    IT IS USED, THAT DET(A*A(TRANSPOSED))=DET(A)**2 AND               C
C    A IS SYMMETRIC. A WILL BE DECOMPOSED FOR EVERY TYPE.              C
C                                                                      C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C
C .. PARAMETER LIST ..
C
      INTEGER LMAXD,LMMAXD,NREPD,NSEC,NTREFD,NATYPD,LMX,NATOMD
      INTEGER NTPERD,NSPIND,IEMXD
      PARAMETER (iemxd=150)     
      COMPLEX*16 CZERO,CONE
      PARAMETER (NATYPD=38,NTREFD=1,NATOMD=102,NTPERD=NATYPD-NTREFD,
     +          NSPIND=2)      
      PARAMETER (nsec=689,NREPD=4)
      PARAMETER (lmaxd=4,LMX=LMAXD+1)
      PARAMETER (LMMAXD=LMX**2)
      PARAMETER (CONE=(1.0D0,0.0D0))
      PARAMETER (CZERO=(0.0D0,0.0D0))
C
C .. VARIABLE LIST -- CALL STATEMENT -- ..
C
      INTEGER    LMAX,NATPS,NATREF,NEND,NATPER,LMAXSQ,NREP,INS
      INTEGER    NDIM(NREPD),IREF(NTPERD),IRMIN(NATYPD),
     +           ASARY(NREPD,NSPIND),ISPIN,IE,NDG(NREPD)
      COMPLEX*16 AR(LMMAXD,LMMAXD,NATYPD)
      COMPLEX*16 DETALF(NATYPD,IEMXD,NSPIND)
C
C .. VARIABLE LIST -- SUBROUTINE -- ..
C      
      COMPLEX*16 ALPHA(NSEC,NSEC),BR(LMMAXD,LMMAXD,NATYPD)
      INTEGER I1,I2,II,LM1,LM2,IPVT(NSEC),INFO,IHOST,NP,NDIMNP,I
      INTEGER SUM,NSHELL(NTPERD)
      COMPLEX*16 DETN,DET,ALPHAH(NSEC,NSEC),DETH,DETNH
      COMPLEX*16 HR(LMMAXD,LMMAXD,NATYPD)
      COMPLEX*16 DR(LMMAXD,LMMAXD,NATYPD)
C
C .. TEST LIST ..
C
      COMPLEX*16 CHELP(LMMAXD,LMMAXD),CR(LMMAXD,LMMAXD,NATYPD)
      COMPLEX*16 DTB(LMMAXD,LMMAXD,NTPERD)
      REAL*8     SA,SD,BRA
      DO I2=NATREF+1,NATYPD
          IHOST=IREF(I2-NATREF)
          HR=(0.0d0,0.0d0)
         BR=(0.0D0,0.0D0)
         DO I1=1,NATYPD
            DO LM1=1,LMMAXD
               BR(LM1,LM1,I1)=CONE
               HR(LM1,LM1,I1)=CONE
            END DO
         END DO
         DO LM1=1,LMMAXD
            DO LM2=1,LMMAXD
               BR(LM1,LM2,I2)=AR(LM1,LM2,I2)
               HR(LM1,LM2,I2)=AR(LM1,LM2,IHOST)
               CHELP(LM1,LM2)=BR(LM1,LM2,I2)
            END DO
         END DO
         CALL ZGEMM('N','T',LMAXSQ,LMAXSQ,LMAXSQ,CONE,
     +   BR(1,1,I2),LMMAXD,
     +   BR(1,1,I2),LMMAXD,CZERO,CR(1,1,I2),LMMAXD)
         CALL ZGEMM('N','T',LMAXSQ,LMAXSQ,LMAXSQ,CONE,
     +   HR(1,1,I2),LMMAXD,
     +   HR(1,1,I2),LMMAXD,CZERO,DR(1,1,I2),LMMAXD)
         DO LM1=1,LMMAXD
            DO LM2=1,LMMAXD
               BR(LM1,LM2,I2)=CR(LM1,LM2,I2)
               HR(LM1,LM2,I2)=DR(LM1,LM2,I2)
            END DO
         END DO
         DETN=CONE
         DETNH=CONE
         DO NP=1,NREP
          IF (ASARY(NP,ISPIN) .EQ. NP) THEN
             NDIMNP=NDIM(NP)
cd             WRITE (*,FMT='(7I5)') INS,LMAX,NATREF,NATPS,NEND,NP,NDG(NP)
             CALL SYTN1(ALPHA,INS,LMAX,NATREF,NATPS,NEND,BR,
     +            IRMIN,NP,NDIM,NREP,.TRUE.)
             CALL ZGETRF(NDIMNP,NDIMNP,ALPHA,NSEC,IPVT,INFO)
             DET = CONE
             DO I = 1,NDIMNP
               IF (IPVT(I).NE.I) DET = -DET
               DET = ALPHA(I,I)*DET
             END DO
             CALL SYTN1(ALPHAH,INS,LMAX,NATREF,NATPS,NEND,HR,
     +            IRMIN,NP,NDIM,NREP,.TRUE.)
             CALL ZGETRF(NDIMNP,NDIMNP,ALPHAH,NSEC,IPVT,INFO)
             DETH = CONE
             DO I = 1,NDIMNP
               IF (IPVT(I).NE.I) DETH = -DETH
               DETH = ALPHAH(I,I)*DETH
             END DO
             DETN=DETN*(DET**(REAL(NDG(NP))))
             DETNH=DETNH*(DETH**(REAL(NDG(NP))))
         END IF
         END DO
         DETN=DETN**(1.0D0/REAL(NSHELL(I2-NATREF)))
         DETNH=DETNH**(1.0D0/REAL(NSHELL(I2-NATREF)))
         DETALF(IHOST,IE,ISPIN)=CDSQRT(DETNH) 
         IF (ABS(DETN-CONE) .LT. 10D-6) THEN 
           DETN=DETALF(IHOST,IE,ISPIN)
           DETALF(I2,IE,ISPIN)=DETN
         ELSE
           DETALF(I2,IE,ISPIN)=CDSQRT(DETN)
         END IF
       END DO
       RETURN
       END
