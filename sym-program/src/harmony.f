      SUBROUTINE HARMONY
C-----------------------------------------------------------------------
C
C                   H A R M O N Y - P R O G R A M
C
C-----------------------------------------------------------------------
C
C THIS PROGRAM DETERMINES THE SYMMETRIZING COEFFICIENTS FOR A CLUSTER OF
C ATOMS WITH SEVERAL SHELLS.
C IT WORKS IN A REAL SPHERICAL HARMONICS BASIS.
C
C CHANGES INTRODUCED BY R. ZELLER:
C           1) ADAPTATION FOR SEVERAL SHELLS         IN 1987
C           2) REMOVAL OF COMMON BLOCKS              IN 1990
C           3) INTRODUCTION OF PARAMETER STATEMENTS  IN 1990
C           4) ADAPTATION FOR SHELLS OF 48 ATOMS     IN 1990
C           5) INTRODUCTION OF COMMENTS              IN 1990
C
C-----------------------------------------------------------------------
C
C DESCRIPTION OF INPUT:
C     NSHELL :  NUMBER OF SHELLS
C     L1     :  MAXIMUM ANGULAR MOMENTUM
C     NREPOS :  NUMBER OF REPRESENTATIONS
C     KTAU   :  ORDER OF THE SYMMETRY GROUP
C     MTAU   :  NUMBER OF ATOMS IN THE CONSIDERED CLUSTER SHELL
C     INVER  :  NUMBER OF DIRECT SYMMETRY OPERATIONS
C               KT>INVER INDICATES COMBINATION OF DIRECT OPERATION AND
C               INVERSION
C     NOORTH :  NOORTH.NE.0 => COEFFICIENTS ARE NOT NORMALIZED
C     IREP   :  IREP = 0 => SUBROUTINE TMX WITHOUT ANY EFFECT
C     IPUN   :  PUNCH PROJECTIONS ON REPRESENTATIONS WITH DIMENSIONS
C               LESS OR EQUAL TO IPUN
C
C      READ(12,501) ((R(I,M),I=1,3),NHOST(M),NATOM(M),NIMP(M),M=1,MTAU)
C120   READ(12,359) NDIM
C      READ(12,420)TITLE
C      READ(12,420) NGO
C
C-----------------------------------------------------------------------
      IMPLICIT REAL*8 (A-H,O-Z)
      PARAMETER (LMAX=48)
      PARAMETER (N48=48)
      PARAMETER (NATMX=1061,NOSHLL=72)
      DIMENSION R(3,N48)
      INTEGER*4 NNOS(NOSHLL),IFILE,NBAS,NBASOS,NDG,NBASUM(10)
      DIMENSION TITLE(20)
      DIMENSION NATOM(N48),NHOST(N48),NIMP(N48),NSHLLF(NOSHLL)
      DIMENSION MATOM(NATMX),MHOST(NATMX),MIMP(NATMX),RM(3,NATMX)
      CHARACTER*50 PGFILE
      READ (12,FMT='(A50)') PGFILE
      OPEN (14,FILE='harmony_output',FORM='formatted')
      OPEN (18,FILE=PGFILE,FORM='formatted')
      OPEN (19,FILE='symm_coef',FORM='formatted')
      READ(12,504) NSHELL,L1,NREPOS
      IF(L1.GT.LMAX) THEN
      WRITE(14,1076)
      STOP 66
 1076 FORMAT(' LMAX TOO LARGE ')
                           ENDIF
      IF(NSHELL.GT.NOSHLL) THEN
      WRITE(14,1077)
      STOP 66
 1077 FORMAT(' TOO MANY SHELLS ')
                           ENDIF
 504  FORMAT(8X,I2,8X,I2,8X,I2,8X,I2,8X,I2)
      IF(NREPOS.EQ.0) NREPOS=10
      DO 99 JREP=1,NREPOS
 99   NBASUM(JREP)=0
      DO 1 I=1,NSHELL
      IFILE=20+I
      OPEN(IFILE,form='formatted')
      WRITE(14,*) IFILE
      CALL HARMY(R,IFILE,MTAU,NATOM,NHOST,NIMP,L1,NBASUM)
      REWIND IFILE
      NSHLLF(I)=MTAU
      DO 5 J=1,MTAU
      NAT=NATOM(J)
      RM(1,NAT)=R(1,J)
      RM(2,NAT)=R(2,J)
      RM(3,NAT)=R(3,J)
      MHOST(NAT)=NHOST(J)
      MATOM(NAT)=NATOM(J)
      MIMP(NAT)=NIMP(J)
    5 CONTINUE
      IF(I.EQ.NSHELL) NATMAX=NATOM(MTAU)
    1 CONTINUE
      NREP=NREPOS
      DO 98 JREP=1,NREPOS
      IF(NBASUM(JREP).EQ.0) NREP=NREP-1
 98   CONTINUE
      WRITE(19,362) NREP,NATMAX,L1,NSHELL,(NSHLLF(I),I=1,NSHELL)
c     WRITE(19,501)
c    1 ((RM(I,M),I=1,3),MHOST(M),MATOM(M),MIMP(M),M=1,NATMAX)
 501  FORMAT(3F10.6,I1,I4,I5)
C---------------------------------------------------------------------
C     READING DATA FOR DIFFERENT SHELLS AND WRITE THEM TOGETHER
C---------------------------------------------------------------------
      NBMAX=0
      NCOMAX=1
      DO 4 J=1,NREPOS
      NBASOS=0
      DO 2 I=1,NSHELL
      IFILE=20+I
      READ(IFILE,5300) NBAS,NDG,(TITLE(K),K=1,4)
      NDGOS=NDG/2
      NNOS(I)=NBAS*NDGOS
      NBASOS=NBASOS+NBAS
    2 CONTINUE
      IF(NBASOS.NE.0)
     1  WRITE(19,5300) NBASOS,NDG,(TITLE(K),K=1,4)
      DO 3 I=1,NSHELL
      IFILE=20+I
      IF(NNOS(I).EQ.0) GO TO 3
      DO 3260 K=1,NNOS(I)
      READ(IFILE,361) LOS,NMSOS,NCOLOS
      IF(NMSOS.GT.NBMAX) NBMAX=NMSOS
      IF(NCOLOS.GT.NCOMAX) NCOMAX=NCOLOS
      WRITE(19,361) LOS,NMSOS,NCOLOS
c     WRITE(8,361) LOS,NMSOS,NCOLOS
      DO 3240 L=1,NMSOS
      READ(IFILE,520) MOS,ICSOS,NATOS,COEFOS
      WRITE(19,520) MOS,ICSOS,NATOS,COEFOS
3240  CONTINUE
3260  CONTINUE
    3 CONTINUE
    4 CONTINUE
      WRITE(14,771) NBMAX,NCOMAX
c     WRITE(8,771) NBMAX,NCOMAX
 771  FORMAT(' MAXIMUM NO. OF BASIS FUNCTIONS:',I5,
     1     ' MAXIMUM NO. OF COLUMNS:',I5)
      DO 111 I=1,NSHELL
      IFILE=20+I
      CLOSE(IFILE,status='delete')
 111  CONTINUE
      CLOSE(17,status='delete')
      CLOSE(18)
      RETURN
361   FORMAT (3I5)
362   FORMAT (8I5)
520   FORMAT (3I5,F17.14)
5300  FORMAT (I5,I5,6X,8A4)
      END
